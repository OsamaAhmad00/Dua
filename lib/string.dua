import "vector.dua"

nomangle long strlen(str string);

class String : Vector<i8>
{
    typealias size_t = long;

    constructor(str string) : Super(strlen(string) + 1)
    {
        for (size_t i = 0; i < _capacity; i++)
            buffer[i] = string[i];
        _size = _capacity;
        buffer[_size] = '\0';
    }

    constructor(size_t n, byte c) : Super(n + 1, c)
    {
        buffer[n] = '\0';
    }

    constructor(str string, bool is_const, bool is_owned) : Super(string, strlen(string) + 1, is_const, is_owned)
    {
        // This assumes that the string is already null terminated
    }

    String substring(size_t from, size_t upto)
    {
        size_t n = upto - from;

        var buffer = new[n + 1] byte;

        for (size_t i = 0; i < n; i++)
            buffer[i] = self[from + i];

        String result(buffer, false, true);

        return result;
    }

    String infix +(String other)
    {
        return self + other.buffer;
    }

    String infix +(str other)
    {
        size_t self_size = size();
        size_t other_size = strlen(other);

        size_t sum = self_size + other_size;

        str buffer = new[self_size + other_size + 1] byte;

        for (long i = 0; i < self_size; i++)
            buffer[i] = self[i];
        for (long i = 0; i < other_size; i++)
            buffer[self_size + i] = other[i];
        buffer[sum] = '\0';

        String result(buffer, false, true);

        return result;
    }

    // Excluding the null terminator
    size_t size() { return _size - 1; }
    size_t capacity() { return _capacity - 1; }

    str c_str() { return buffer; }
}

String infix +(str other, String self)
{
    long self_size = self.size();
    long other_size = strlen(other);

    long sum = self_size + other_size;
    str buffer = new[sum + 1] byte;

    for (long i = 0; i < other_size; i++)
        buffer[i] = other[i];
    for (long i = 0; i < self_size; i++)
        buffer[i + other_size] = self[i];
    buffer[sum] = '\0';

    String result(buffer, false, true);

    return result;
}
